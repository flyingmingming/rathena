-	script	cmd_ascendance_points_shop	-1,{

OnBuyItem:
	.@size = getarraysize(@bought_nameid);

	for (.@i = 0; .@i < .@size; .@i++) {
		.@j = inarray(.Shop, @bought_nameid[.@i]);
		.@cost += (.Shop[.@j+1] * @bought_quantity[.@i]);
	}
	if (.@cost > #ascendance_points)
    {
        dispbottom "You don't have enough Ascendance Points.";
        deletearray @bought_nameid[0], .@size;
        deletearray @bought_quantity[0], .@size;         
        end;
    }
	else if (!checkweight2(@bought_nameid,@bought_quantity)) 
	{
		dispbottom "You cannot carry more items because you are overweight.",0xFF0000;
        deletearray @bought_nameid[0], .@size;
        deletearray @bought_quantity[0], .@size;         
        end;		
	}
	else 
    {
		
		for (.@i = 0; .@i < .@size; .@i++) {
			getitem @bought_nameid[.@i], @bought_quantity[.@i];
		}
		#ascendance_points -= .@cost;
	}
	deletearray @bought_nameid[0], .@size;
	deletearray @bought_quantity[0], .@size; 

	dispbottom "" + F_InsertComma(.@cost) + " Ascendance points has been removed.";
    dispbottom "You now have " + F_InsertComma(#ascendance_points) + " Ascendance Points remaining.";
end;

OnInit:
	bindatcmd "pointshop",strnpcinfo(0)+"::OnAtcommand";

	deletearray .Shop[0],getarraysize(.Shop);
	setarray .Shop[0],      // Reward items: <ID>,<point cost> (about 10~20 points per hunt).
		14001,1, //recruit siege box
		513,1,
		514,1,
		538,5,
		539,5,
		558,10,
		34003,100; //vip 1day


    npcshopdelitem "points_shop",512;
	for (.@i = 0; .@i < getarraysize(.Shop); .@i += 2)
    {
        npcshopadditem "points_shop", .Shop[.@i], .Shop[.@i+1];
    }




	end;

OnAtcommand:
    callshop "points_shop",1;
    npcshopattach "points_shop";
    end;

}

-	shop	points_shop	-1,512:-1
